[@main]
#IF
EQUAL A3 <$USERNAME>
#SAY
　请问你需要进行什么操作？\ \
{你申请时的信息=FCO=243}\
　申请人物名称：{<$STR(A3)>=FCO=242}\
　申请人物等级：{<$STR(G13)>=FCO=242}\
　申请人物魔法：{<$STR(G14)>=FCO=242}\
　保持纪录时间：{<$STR(G17)>小时=FCO=242}\
　{上次维护时间=FCo=250}：{<$STR(G15)>小时之前=FCO=242}\ \
<&更新信息/@天下第一_数据更新>\
<&维护雕像/@天下第一_雕像维护>\
<&更换形像/@天下第一_更换形像>\
<&关闭/@exit>\
#ELSESAY
　想为自己树立一尊雕像吗？从此扬名天\
下！现在机会来了，赶快申请吧！\
　{申请成功后，可以自由设定雕像的样子=FCO=250}\
{完全为你量身打造！=FCO=250}\ \
{本届法神信息=FCO=243}\
　申请人物名称：{<$STR(A3)>=FCO=242}\
　申请人物等级：{<$STR(G13)>=FCO=242}\
　申请人物魔法：{<$STR(G14)>=FCO=242}\
　保持纪录时间：{<$STR(G17)>小时=FCO=242}\ \
<&我要申请/@天下第一_我要申请>\
<&申请条件/@天下第一_申请规则>\
<&关闭/@exit>\

[@天下第一_申请规则]
{申请天下第一法神条件：=FCO=243}\ \
　魔法高于本届法神的魔法。\
{或者=FCO=243}\
　魔法等于本届法神的魔法，并且等级高\
于本届法神的等级。\ \
<&返回/@系统_标准NPC头_返回>\

[@天下第一_我要申请]
#IF
NOT CHECKJOB WIZARD
#ACT
MESSAGEBOX 申请失败，该雕像只有魔法师职业才能申请！
BREAK

#IF
CHECKMC ? 0 > <$STR(G14)>
#ACT
GOTO @天下第一_申请成功
BREAK

#IF
CHECKMC ? 0 = <$STR(G14)>
CHECKLEVELEX > <$STR(G13)>
#ACT
GOTO @天下第一_申请成功
#ELSEACT
MESSAGEBOX 申请失败，你的魔法数据没有超过本届法神的数据！

[@天下第一_申请成功]
#IF
#SAY
　{恭喜你，申请成功！=FCO=250}\ \
　你现在可以对雕像进行维护和管理。雕\
像的造形是可以自由设定的。\
　{另外，雕像每72个小时之内必需进行维=FCO=243}\
{护一次，不然雕像将破碎，破碎后天下第=FCO=243}\
{一将被系统收回，需要重新申请。=FCO=243}\ \

<&返回/@系统_标准NPC头_返回>\
#ACT
MOV A3 <$USERNAME>
MOV G12 <$EFFIGYSTATE>
MOV G13 <$LEVEL>
MOV G14 <$MAXMC>
MOV G15 0
MOV G16 40
MOV G17 0
REFSHOWNAME
SETEFFIGYSTATE <$STR(G12)> 40
SENDMSG 0 [#6FFFF/8#6公告#5]：恭喜[#7<$USERNAME>#7]成功申请了#6FFFF/8#6天下第一法神#5，申请数据[#6FFFF/8#6等级(<$STR(G13)>)，魔法(<$STR(G14)>)#5]

[@天下第一_更换形像]
　雕像的样子是跟据人物当前的着装来显\
示。雕像的衣服、武器和发型是跟据你当\
前的穿着来显示，人物的造形你可以进行\
选择。请选择你要雕刻的造形！\ \
<&站立造形/@天下第一_更换形像_确定更新(40)>\
<&走路造形/@天下第一_更换形像_确定更新(107)>\
<&跑步造形/@天下第一_更换形像_确定更新(173)>\
<&魔法造形一/@天下第一_更换形像_确定更新(240)>\
<&魔法造形二/@天下第一_更换形像_确定更新(436)>\
<&魔法造形三/@天下第一_更换形像_确定更新(514)>\
<&返回/@系统_标准NPC头_返回>\

[@天下第一_更换形像_确定更新]
#IF
#ACT
MOV G12 <$EFFIGYSTATE>
MOV G16 <$STR(T0)>
SETEFFIGYSTATE <$STR(G12)> <$STR(G16)>
MESSAGEBOX 恭喜你，雕像打造成功！

[@天下第一_数据更新]
{当前雕像信息=FCO=243}\
　雕像人物等级：{<$STR(G13)>=FCO=242}\
　雕像人物魔法：{<$STR(G14)>=FCO=242}\ \
{你当前人物信息=FCO=243}\
　人物等级：{<$LEVEL>=FCO=242}\
　人物魔法：{<$MAXMC>=FCO=242}\ \
　当你人物魔法高于雕像魔法{或者=FCO=243}人物魔\
法等于雕像魔法并且等级高于雕像等级时，\
既可进行数据更新。\ \
<&确认更新/@天下第一_数据更新_确定更新>\
<&返回/@系统_标准NPC头_返回>\

[@天下第一_数据更新_确定更新]
#IF
CHECKMC ? 0 > <$STR(G14)>
#ACT
MOV G13 <$LEVEL>
MOV G14 <$MAXMC>
SENDMSG 0 [#6FFFF/8#6公告#5]：天下第一法神[#7<$USERNAME>#7]更新了雕像资料，最新数据[#6FFFF/8#6等级(<$STR(G13)>)，魔法(<$STR(G14)>)#5]
MESSAGEBOX 恭喜你，数据已成功更新！
GOTO @天下第一_数据更新
BREAK

#IF
CHECKMC ? 0 = <$STR(G14)>
CHECKLEVELEX > <$STR(G13)>
#ACT
MOV G13 <$LEVEL>
MOV G14 <$MAXMC>
SENDMSG 0 [#6FFFF/8#6公告#5]：天下第一法神[#7<$USERNAME>#7]更新了雕像资料，最新数据[#6FFFF/8#6等级(<$STR(G13)>)，魔法(<$STR(G14)>)#5]
MESSAGEBOX 恭喜你，数据已成功更新！
GOTO @天下第一_数据更新
#ELSEACT
MESSAGEBOX 你目前的信息不需要进行更新！

[@天下第一_雕像维护]
　{上次维护时间=FCO=243}：{<$STR(G15)>小时之前=FCO=242}\ \
　雕像每72小时之内必需维护一次，不然\
雕像将破碎。天下第一也将被系统收回，\
需要重新申请。\ \
<&确认维护/@天下第一_雕像维护_确定更新>\
<&返回/@系统_标准NPC头_返回>\

[@天下第一_雕像维护_确定更新]
#IF
SMALL G15 1
#ACT
MESSAGEBOX 雕像目前不需要进行维护！
BREAK

#IF
EQUAL A3 <$USERNAME>
#ACT
MOV G15 0
MESSAGEBOX 恭喜你，雕像已成功维护，下次最迟维护时间72小时之内！
GOTO @天下第一_雕像维护


[@天下第一_时间统计]
;当计录等级大于0才计算持续时间
#IF
SMALL G13 1
#ACT
BREAK

#IF
LARGE G15 70
#ACT
SENDMSG 0 [#6FFFF/8#6公告#5]：天下第一法神[#7<$STR(A3)>#7]由于长时间没有对雕像进行管理，雕像已经破碎。大家赶快来申请天下第一法神吧！
MOV A3 (虚位以待)
MOV G12 -1
MOV G13 0
MOV G14 0
MOV G15 0
REFSHOWNAME
SETEFFIGYSTATE -1 0
#ELSEACT
INC G15 1
INC G17 1
DELAYGOTO 3600 @天下第一_时间统计 时间统计

[~Initialize]
;如果计录等级小于1则表示没有人申请
#IF
SMALL G13 1
#ACT
MOV A3 (虚位以待)
MOV G12 -1
MOV G13 0
MOV G14 0
MOV G15 0
MOV G16 0
MOV G17 0
DELAYGOTO 3600 @天下第一_时间统计 时间统计
#ELSEACT
SETEFFIGYSTATE <$STR(G12)> <$STR(G16)>
DELAYGOTO 3600 @天下第一_时间统计 时间统计
