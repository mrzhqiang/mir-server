[@main]
#IF
EQUAL A2 <$USERNAME>
#SAY
　请问你需要进行什么操作？\ \
{你申请时的信息=FCO=243}\
　申请人物名称：{<$STR(A2)>=FCO=242}\
　申请人物等级：{<$STR(G7)>=FCO=242}\
　申请人物攻击：{<$STR(G8)>=FCO=242}\
　保持纪录时间：{<$STR(G11)>小时=FCO=242}\
　{上次维护时间=FCo=250}：{<$STR(G9)>小时之前=FCO=242}\ \
<&更新信息/@天下第一_数据更新>\
<&维护雕像/@天下第一_雕像维护>\
<&更换形像/@天下第一_更换形像>\
<&关闭/@exit>\
#ELSESAY
　想为自己树立一尊雕像吗？从此扬名天\
下！现在机会来了，赶快申请吧！\
　{申请成功后，可以自由设定雕像的样子=FCO=250}\
{完全为你量身打造！=FCO=250}\ \
{本届战圣信息=FCO=243}\
　申请人物名称：{<$STR(A2)>=FCO=242}\
　申请人物等级：{<$STR(G7)>=FCO=242}\
　申请人物攻击：{<$STR(G8)>=FCO=242}\
　保持纪录时间：{<$STR(G11)>小时=FCO=242}\ \
<&我要申请/@天下第一_我要申请>\
<&申请条件/@天下第一_申请规则>\
<&关闭/@exit>\


[@天下第一_申请规则]
{申请天下第一战圣条件：=FCO=243}\ \
　攻击高于本届战圣的攻击。\
{或者=FCO=243}\
　攻击等于本届战圣的攻击，并且等级高\
于本届战圣的等级。\ \
<&返回/@系统_标准NPC头_返回>\

[@天下第一_我要申请]
#IF
NOT CHECKJOB WARR
#ACT
MESSAGEBOX 申请失败，该雕像只有战士职业才能申请！
BREAK

#IF
CHECKDC ? 0 > <$STR(G8)>
#ACT
GOTO @天下第一_申请成功
BREAK

#IF
CHECKDC ? 0 = <$STR(G8)>
CHECKLEVELEX > <$STR(G7)>
#ACT
GOTO @天下第一_申请成功
#ELSEACT
MESSAGEBOX 申请失败，你的攻击数据没有超过本届战圣的数据！


[@天下第一_申请成功]
#IF
#SAY
　{恭喜你，申请成功！=FCO=250}\ \
　你现在可以对雕像进行维护和管理。雕\
像的造形是可以自由设定的。\
　{另外，雕像每72个小时之内必需进行维=FCO=243}\
{护一次，不然雕像将破碎，破碎后天下第=FCO=243}\
{一将被系统收回，需要重新申请。=FCO=243}\ \

<&返回/@系统_标准NPC头_返回>\
#ACT
MOV A2 <$USERNAME>
MOV G6 <$EFFIGYSTATE>
MOV G7 <$LEVEL>
MOV G8 <$MAXDC>
MOV G9 0
MOV G10 40
MOV G11 0
REFSHOWNAME
SETEFFIGYSTATE <$STR(G6)> 40
SENDMSG 0 [#6FFFF/8#6公告#5]：恭喜[#7<$USERNAME>#7]成功申请了#6FFFF/8#6天下第一战圣#5，申请数据[#6FFFF/8#6等级(<$STR(G7)>)，攻击(<$STR(G8)>)#5]

[@天下第一_更换形像]
　雕像的样子是跟据人物当前的着装来显\
示。雕像的衣服、武器和发型是跟据你当\
前的穿着来显示，人物的造形你可以进行\
选择。请选择你要雕刻的造形！\ \
<&站立造形/@天下第一_更换形像_确定更新(40)>\
<&走路造形/@天下第一_更换形像_确定更新(107)>\
<&跑步造形/@天下第一_更换形像_确定更新(173)>\
<&攻击造形一/@天下第一_更换形像_确定更新(240)>\
<&攻击造形二/@天下第一_更换形像_确定更新(436)>\
<&攻击造形三/@天下第一_更换形像_确定更新(514)>\
<&返回/@系统_标准NPC头_返回>\

[@天下第一_更换形像_确定更新]
#IF
#ACT
MOV G6 <$EFFIGYSTATE>
MOV G10 <$STR(T0)>
SETEFFIGYSTATE <$STR(G6)> <$STR(G10)>
MESSAGEBOX 恭喜你，雕像打造成功！

[@天下第一_数据更新]
{当前雕像信息=FCO=243}\
　雕像人物等级：{<$STR(G7)>=FCO=242}\
　雕像人物攻击：{<$STR(G8)>=FCO=242}\ \
{你当前人物信息=FCO=243}\
　人物等级：{<$LEVEL>=FCO=242}\
　人物攻击：{<$MAXDC>=FCO=242}\ \
　当你人物攻击高于雕像攻击{或者=FCO=243}人物攻\
击等于雕像攻击并且等级高于雕像等级时，\
既可进行数据更新。\ \
<&确认更新/@天下第一_数据更新_确定更新>\
<&返回/@系统_标准NPC头_返回>\

[@天下第一_数据更新_确定更新]
#IF
CHECKDC ? 0 > <$STR(G8)>
#ACT
MOV G7 <$LEVEL>
MOV G8 <$MAXDC>
SENDMSG 0 [#6FFFF/8#6公告#5]：天下第一战圣[#7<$USERNAME>#7]更新了雕像资料，最新数据[#6FFFF/8#6等级(<$STR(G7)>)，攻击(<$STR(G8)>)#5]
MESSAGEBOX 恭喜你，数据已成功更新！
GOTO @天下第一_数据更新
BREAK

#IF
CHECKDC ? 0 = <$STR(G8)>
CHECKLEVELEX > <$STR(G7)>
#ACT
MOV G7 <$LEVEL>
MOV G8 <$MAXDC>
SENDMSG 0 [#6FFFF/8#6公告#5]：天下第一战圣[#7<$USERNAME>#7]更新了雕像资料，最新数据[#6FFFF/8#6等级(<$STR(G7)>)，攻击(<$STR(G8)>)#5]
MESSAGEBOX 恭喜你，数据已成功更新！
GOTO @天下第一_数据更新
#ELSEACT
MESSAGEBOX 你目前的信息不需要进行更新！

[@天下第一_雕像维护]
　{上次维护时间=FCO=243}：{<$STR(G9)>小时之前=FCO=242}\ \
　雕像每72小时之内必需维护一次，不然\
雕像将破碎。天下第一也将被系统收回，\
需要重新申请。\ \
<&确认维护/@天下第一_雕像维护_确定更新>\
<&返回/@系统_标准NPC头_返回>\

[@天下第一_雕像维护_确定更新]
#IF
SMALL G9 1
#ACT
MESSAGEBOX 雕像目前不需要进行维护！
BREAK

#IF
EQUAL A2 <$USERNAME>
#ACT
MOV G9 0
MESSAGEBOX 恭喜你，雕像已成功维护，下次最迟维护时间72小时之内！
GOTO @天下第一_雕像维护


[@天下第一_时间统计]
;当计录等级大于0才计算持续时间
#IF
SMALL G7 1
#ACT
BREAK

#IF
LARGE G9 70
#ACT
SENDMSG 0 [#6FFFF/8#6公告#5]：天下第一战圣[#7<$STR(A2)>#7]由于长时间没有对雕像进行管理，雕像已经破碎。大家赶快来申请天下第一战圣吧！
MOV A2 (虚位以待)
MOV G6 -1
MOV G7 0
MOV G8 0
MOV G9 0
REFSHOWNAME
SETEFFIGYSTATE -1 0
#ELSEACT
INC G9 1
INC G11 1
DELAYGOTO 3600 @天下第一_时间统计 时间统计

[~Initialize]
;如果计录等级小于1则表示没有人申请
#IF
SMALL G7 1
#ACT
MOV A2 (虚位以待)
MOV G6 -1
MOV G7 0
MOV G8 0
MOV G9 0
MOV G10 0
MOV G11 0
DELAYGOTO 3600 @天下第一_时间统计 时间统计
#ELSEACT
SETEFFIGYSTATE <$STR(G6)> <$STR(G10)>
DELAYGOTO 3600 @天下第一_时间统计 时间统计
