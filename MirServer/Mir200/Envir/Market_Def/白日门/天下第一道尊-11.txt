[@main]
#IF
EQUAL A4 <$USERNAME>
#SAY
　请问你需要进行什么操作？\ \
{你申请时的信息=FCO=243}\
　申请人物名称：{<$STR(A4)>=FCO=242}\
　申请人物等级：{<$STR(G19)>=FCO=242}\
　申请人物道术：{<$STR(G20)>=FCO=242}\
　保持纪录时间：{<$STR(G23)>小时=FCO=242}\
　{上次维护时间=FCo=250}：{<$STR(G21)>小时之前=FCO=242}\ \
<&更新信息/@天下第一_数据更新>\
<&维护雕像/@天下第一_雕像维护>\
<&更换形像/@天下第一_更换形像>\
<&关闭/@exit>\
#ELSESAY
　想为自己树立一尊雕像吗？从此扬名天\
下！现在机会来了，赶快申请吧！\
　{申请成功后，可以自由设定雕像的样子=FCO=250}\
{完全为你量身打造！=FCO=250}\ \
{本届道尊信息=FCO=243}\
　申请人物名称：{<$STR(A4)>=FCO=242}\
　申请人物等级：{<$STR(G19)>=FCO=242}\
　申请人物道术：{<$STR(G20)>=FCO=242}\
　保持纪录时间：{<$STR(G23)>小时=FCO=242}\ \
<&我要申请/@天下第一_我要申请>\
<&申请条件/@天下第一_申请规则>\
<&关闭/@exit>\

[@天下第一_申请规则]
{申请天下第一道尊条件：=FCO=243}\ \
　道术高于本届道尊的道术。\
{或者=FCO=243}\
　道术等于本届道尊的道术，并且等级高\
于本届道尊的等级。\ \
<&返回/@系统_标准NPC头_返回>\

[@天下第一_我要申请]
#IF
NOT CHECKJOB TAOS
#ACT
MESSAGEBOX 申请失败，该雕像只有道士职业才能申请！
BREAK

#IF
CHECKSC ? 0 > <$STR(G20)>
#ACT
GOTO @天下第一_申请成功
BREAK

#IF
CHECKSC ? 0 = <$STR(G20)>
CHECKLEVELEX > <$STR(G19)>
#ACT
GOTO @天下第一_申请成功
#ELSEACT
MESSAGEBOX 申请失败，你的道术数据没有超过本届道尊的数据！


[@天下第一_申请成功]
#IF
#SAY
　{恭喜你，申请成功！=FCO=250}\ \
　你现在可以对雕像进行维护和管理。雕\
像的造形是可以自由设定的。\
　{另外，雕像每72个小时之内必需进行维=FCO=243}\
{护一次，不然雕像将破碎，破碎后天下第=FCO=243}\
{一将被系统收回，需要重新申请。=FCO=243}\ \

<&返回/@系统_标准NPC头_返回>\
#ACT
MOV A4 <$USERNAME>
MOV G18 <$EFFIGYSTATE>
MOV G19 <$LEVEL>
MOV G20 <$MAXSC>
MOV G21 0
MOV G22 40
MOV G23 0
REFSHOWNAME
SETEFFIGYSTATE <$STR(G18)> 40
SENDMSG 0 [#6FFFF/8#6公告#5]：恭喜[#7<$USERNAME>#7]成功申请了#6FFFF/8#6天下第一道尊#5，申请数据[#6FFFF/8#6等级(<$STR(G19)>)，道术(<$STR(G20)>)#5]

[@天下第一_更换形像]
　雕像的样子是跟据人物当前的着装来显\
示。雕像的衣服、武器和发型是跟据你当\
前的穿着来显示，人物的造形你可以进行\
选择。请选择你要雕刻的造形！\ \
<&站立造形/@天下第一_更换形像_确定更新(40)>\
<&走路造形/@天下第一_更换形像_确定更新(107)>\
<&跑步造形/@天下第一_更换形像_确定更新(173)>\
<&道术造形一/@天下第一_更换形像_确定更新(240)>\
<&道术造形二/@天下第一_更换形像_确定更新(436)>\
<&道术造形三/@天下第一_更换形像_确定更新(514)>\
<&返回/@系统_标准NPC头_返回>\

[@天下第一_更换形像_确定更新]
#IF
#ACT
MOV G18 <$EFFIGYSTATE>
MOV G22 <$STR(T0)>
SETEFFIGYSTATE <$STR(G18)> <$STR(G22)>
MESSAGEBOX 恭喜你，雕像打造成功！

[@天下第一_数据更新]
{当前雕像信息=FCO=243}\
　雕像人物等级：{<$STR(G19)>=FCO=242}\
　雕像人物道术：{<$STR(G20)>=FCO=242}\ \
{你当前人物信息=FCO=243}\
　人物等级：{<$LEVEL>=FCO=242}\
　人物道术：{<$MAXSC>=FCO=242}\ \
　当你人物道术高于雕像道术{或者=FCO=243}人物道\
术等于雕像道术并且等级高于雕像等级时，\
既可进行数据更新。\ \
<&确认更新/@天下第一_数据更新_确定更新>\
<&返回/@系统_标准NPC头_返回>\

[@天下第一_数据更新_确定更新]
#IF
CHECKSC ? 0 > <$STR(G20)>
#ACT
MOV G19 <$LEVEL>
MOV G20 <$MAXSC>
SENDMSG 0 [#6FFFF/8#6公告#5]：天下第一道尊[#7<$USERNAME>#7]更新了雕像资料，最新数据[#6FFFF/8#6等级(<$STR(G19)>)，道术(<$STR(G20)>)#5]
MESSAGEBOX 恭喜你，数据已成功更新！
GOTO @天下第一_数据更新
BREAK

#IF
CHECKSC ? 0 = <$STR(G20)>
CHECKLEVELEX > <$STR(G19)>
#ACT
MOV G19 <$LEVEL>
MOV G20 <$MAXSC>
SENDMSG 0 [#6FFFF/8#6公告#5]：天下第一道尊[#7<$USERNAME>#7]更新了雕像资料，最新数据[#6FFFF/8#6等级(<$STR(G19)>)，道术(<$STR(G20)>)#5]
MESSAGEBOX 恭喜你，数据已成功更新！
GOTO @天下第一_数据更新
#ELSEACT
MESSAGEBOX 你目前的信息不需要进行更新！

[@天下第一_雕像维护]
　{上次维护时间=FCO=243}：{<$STR(G21)>小时之前=FCO=242}\ \
　雕像每72小时之内必需维护一次，不然\
雕像将破碎。天下第一也将被系统收回，\
需要重新申请。\ \
<&确认维护/@天下第一_雕像维护_确定更新>\
<&返回/@系统_标准NPC头_返回>\

[@天下第一_雕像维护_确定更新]
#IF
SMALL G21 1
#ACT
MESSAGEBOX 雕像目前不需要进行维护！
BREAK

#IF
EQUAL A4 <$USERNAME>
#ACT
MOV G21 0
MESSAGEBOX 恭喜你，雕像已成功维护，下次最迟维护时间72小时之内！
GOTO @天下第一_雕像维护


[@天下第一_时间统计]
;当计录等级大于0才计算持续时间
#IF
SMALL G19 1
#ACT
BREAK

#IF
LARGE G21 70
#ACT
SENDMSG 0 [#6FFFF/8#6公告#5]：天下第一道尊[#7<$STR(A4)>#7]由于长时间没有对雕像进行管理，雕像已经破碎。大家赶快来申请天下第一道尊吧！
MOV A4 (虚位以待)
MOV G18 -1
MOV G19 0
MOV G20 0
MOV G21 0
REFSHOWNAME
SETEFFIGYSTATE -1 0
#ELSEACT
INC G21 1
INC G23 1
DELAYGOTO 3600 @天下第一_时间统计 时间统计

[~Initialize]
;如果计录等级小于1则表示没有人申请
#IF
SMALL G19 1
#ACT
MOV A4 (虚位以待)
MOV G18 -1
MOV G19 0
MOV G20 0
MOV G21 0
MOV G22 0
MOV G23 0
DELAYGOTO 3600 @天下第一_时间统计 时间统计
#ELSEACT
SETEFFIGYSTATE <$STR(G18)> <$STR(G22)>
DELAYGOTO 3600 @天下第一_时间统计 时间统计
