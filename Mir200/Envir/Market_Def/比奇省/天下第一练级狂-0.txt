[@main]
#IF
EQUAL A1 <$USERNAME>
#SAY
　请问你需要进行什么操作？\ \
{你申请时的信息=FCO=243}\
　申请人物名称：{<$STR(A1)>=FCO=242}\
　申请人物等级：{<$STR(G1)>=FCO=242}\
　申请人物经验：{<$STR(G2)>=FCO=242}\
　保持纪录时间：{<$STR(G5)>小时=FCO=242}\
　{上次维护时间=FCo=250}：{<$STR(G3)>小时之前=FCO=242}\ \
<&更新信息/@天下第一_数据更新>\
<&维护雕像/@天下第一_雕像维护>\
<&更换形像/@天下第一_更换形像>\
<&关闭/@exit>\
#ELSESAY
　想为自己树立一尊雕像吗？从此扬名天\
下！现在机会来了，赶快申请吧！\
　{申请成功后，可以自由设定雕像的样子=FCO=250}\
{完全为你量身打造！=FCO=250}\ \
{本届练级狂信息=FCO=243}\
　申请人物名称：{<$STR(A1)>=FCO=242}\
　申请人物等级：{<$STR(G1)>=FCO=242}\
　申请人物经验：{<$STR(G2)>=FCO=242}\
　保持纪录时间：{<$STR(G5)>小时=FCO=242}\ \
<&我要申请/@天下第一_我要申请>\
<&申请条件/@天下第一_申请规则>\
<&关闭/@exit>\

[@天下第一_申请规则]
{申请天下第一练级狂条件：=FCO=243}\ \
　等级高于本届练级狂的等级。\
{或者=FCO=243}\
　等级等于本届练级狂的等级，并且经验\
值高于本届练级狂的经验。\ \
<&返回/@系统_标准NPC头_返回>\

[@天下第一_我要申请]
#IF
CHECKLEVELEX > <$STR(G1)>
#ACT
GOTO @天下第一_申请成功
BREAK

#IF
CHECKLEVELEX = <$STR(G1)>
CHECKEXP > <$STR(G2)>
#ACT
GOTO @天下第一_申请成功
#ELSEACT
MESSAGEBOX 申请失败，你的等级数据没有超过本届练级狂的数据！


[@天下第一_申请成功]
#IF
#SAY
　{恭喜你，申请成功！=FCO=250}\ \
　你现在可以对雕像进行维护和管理。雕\
像的造形是可以自由设定的。\
　{另外，雕像每72个小时之内必需进行维=FCO=243}\
{护一次，不然雕像将破碎，破碎后天下第=FCO=243}\
{一将被系统收回，需要重新申请。=FCO=243}\ \

<&返回/@系统_标准NPC头_返回>\
#ACT
MOV A1 <$USERNAME>
MOV G0 <$EFFIGYSTATE>
MOV G1 <$LEVEL>
MOV G2 <$EXP>
MOV G3 0
MOV G4 40
MOV G5 0
REFSHOWNAME
SETEFFIGYSTATE <$STR(G0)> 40
SENDMSG 0 [#6FFFF/8#6公告#5]：恭喜[#7<$USERNAME>#7]成功申请了#6FFFF/8#6天下第一练级狂#5，申请数据[#6FFFF/8#6等级(<$STR(G1)>)，经验(<$STR(G2)>)#5]

[@天下第一_更换形像]
　雕像的样子是跟据人物当前的着装来显\
示。雕像的衣服、武器和发型是跟据你当\
前的穿着来显示，人物的造形你可以进行\
选择。请选择你要雕刻的造形！\ \
<&站立造形/@天下第一_更换形像_确定更新(40)>\
<&走路造形/@天下第一_更换形像_确定更新(107)>\
<&跑步造形/@天下第一_更换形像_确定更新(173)>\
<&攻击造形一/@天下第一_更换形像_确定更新(240)>\
<&攻击造形二/@天下第一_更换形像_确定更新(436)>\
<&攻击造形三/@天下第一_更换形像_确定更新(514)>\
<&返回/@系统_标准NPC头_返回>\

[@天下第一_更换形像_确定更新]
#IF
#ACT
MOV G0 <$EFFIGYSTATE>
MOV G4 <$STR(T0)>
SETEFFIGYSTATE <$STR(G0)> <$STR(G4)>
MESSAGEBOX 恭喜你，雕像打造成功！

[@天下第一_数据更新]
{当前雕像信息=FCO=243}\
　雕像人物等级：{<$STR(G1)>=FCO=242}\
　雕像人物经验：{<$STR(G2)>=FCO=242}\ \
{你当前人物信息=FCO=243}\
　人物等级：{<$LEVEL>=FCO=242}\
　人物经验：{<$EXP>=FCO=242}\ \
　当你人物等级高于雕像等级{或者=FCO=243}人物等\
级等于雕像等级并且经验值高于雕像经验\
值时，既可进行数据更新。\ \
<&确认更新/@天下第一_数据更新_确定更新>\
<&返回/@系统_标准NPC头_返回>\

[@天下第一_数据更新_确定更新]
#IF
CHECKLEVELEX > <$STR(G1)>
#ACT
MOV G1 <$LEVEL>
MOV G2 <$EXP>
SENDMSG 0 [#6FFFF/8#6公告#5]：天下第一练级狂[#7<$USERNAME>#7]更新了雕像资料，最新数据[#6FFFF/8#6等级(<$STR(G1)>)，经验(<$STR(G2)>)#5]
MESSAGEBOX 恭喜你，数据已成功更新！
GOTO @天下第一_数据更新
BREAK

#IF
CHECKLEVELEX = <$STR(G1)>
CHECKEXP > <$STR(G2)>
#ACT
MOV G1 <$LEVEL>
MOV G2 <$EXP>
SENDMSG 0 [#6FFFF/8#6公告#5]：天下第一练级狂[#7<$USERNAME>#7]更新了雕像资料，最新数据[#6FFFF/8#6等级(<$STR(G1)>)，经验(<$STR(G2)>)#5]
MESSAGEBOX 恭喜你，数据已成功更新！
GOTO @天下第一_数据更新
#ELSEACT
MESSAGEBOX 你目前的信息不需要进行更新！

[@天下第一_雕像维护]
　{上次维护时间=FCO=243}：{<$STR(G3)>小时之前=FCO=242}\ \
　雕像每72小时之内必需维护一次，不然\
雕像将破碎。天下第一也将被系统收回，\
需要重新申请。\ \
<&确认维护/@天下第一_雕像维护_确定更新>\
<&返回/@系统_标准NPC头_返回>\

[@天下第一_雕像维护_确定更新]
#IF
SMALL G3 1
#ACT
MESSAGEBOX 雕像目前不需要进行维护！
BREAK

#IF
EQUAL A1 <$USERNAME>
#ACT
MOV G3 0
MESSAGEBOX 恭喜你，雕像已成功维护，下次最迟维护时间72小时之内！
GOTO @天下第一_雕像维护


[@天下第一_时间统计]
;当计录等级大于0才计算持续时间
#IF
SMALL G1 1
#ACT
BREAK

#IF
LARGE G3 70
#ACT
SENDMSG 0 [#6FFFF/8#6公告#5]：天下第一练级狂[#7<$STR(A1)>#7]由于长时间没有对雕像进行管理，雕像已经破碎。大家赶快来申请天下第一练级狂吧！
MOV A1 (虚位以待)
MOV G0 -1
MOV G1 0
MOV G2 0
MOV G3 0
REFSHOWNAME
SETEFFIGYSTATE -1 0
#ELSEACT
INC G3 1
INC G5 1
DELAYGOTO 3600 @天下第一_时间统计 时间统计

[~Initialize]
;如果计录等级小于1则表示没有人申请
#IF
SMALL G1 1
#ACT
MOV A1 (虚位以待)
MOV G0 -1
MOV G1 0
MOV G2 0
MOV G3 0
MOV G4 0
MOV G5 0
DELAYGOTO 3600 @天下第一_时间统计 时间统计
#ELSEACT
SETEFFIGYSTATE <$STR(G0)> <$STR(G4)>
DELAYGOTO 3600 @天下第一_时间统计 时间统计
